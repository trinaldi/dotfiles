riml_include 'with_dir'

class CTagsBuilder
  def initialize
    self.is_ctags_builder = true
    self.project_path = ''
  end

  defm needs
    return ['with_dir', 'ctags_command_builder']
  end

  defm clean
    tags = self.get_project_tags()
    if filereadable(tags)
      delete(tags)
    end
  end

  defm generate
    unless self.has_executable()
      return false
    end

    cmd                  = self.build()
    with_dir             = self.get_with_dir()
    [error_code, result] = self.with_dir.run(self, 'run', [cmd])

    return error_code == 0
  end

  defm set_project_path(project_path)
    self.project_path = project_path
  end

  defm get_project_path
    return self.project_path
  end

  defm get_tags_name
    return self.ctags_command_builder.get_tags_name()
  end

  defm get_project_tags
    return "#{self.get_project_path()}/#{self.get_tags_name()}"
  end

  defm get_executable
    return self.ctags_command_builder.get_executable()
  end

  defm has_executable
    return self.ctags_command_builder.has_executable()
  end

  defm has_tags
    return filereadable(self.get_project_tags())
  end

  defm get_with_dir
    with_dir = self.with_dir
    with_dir.set_dir(self.get_project_path())

    return with_dir
  end

  defm run(cmd)
    result = system(cmd)
    return [v:shell_error, result]
  end

  defm build
    return self.ctags_command_builder.build()
  end
end
